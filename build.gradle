plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin:"groovy"
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext {
    versionName = '0.2.2'
    mavenGroupId = 'de.datenwissen.util'
}

group = mavenGroupId
version = versionName

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://repository.apache.org/content/repositories/releases/"
    }
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.1'
    compile ('org.apache.jena:jena-core:3.8.0')
    compile ('io.github.http-builder-ng:http-builder-ng-core:1.0.3')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

publishing {
    publications {
        GroovyrdfPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId mavenGroupId
            artifactId 'groovyrdf'
            version versionName
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'libraries'
        name = 'groovyrdf'
        licenses = ['BSD 3-Clause', 'BSD 2-Clause', 'BSD']
        vcsUrl ='https://github.com/angelo-v/groovyrdf.git'
        version {
            name = versionName
            released  = new Date()
            vcsTag = versionName
        }
    }
    publications = ['GroovyrdfPublication']
    publish = true
    override = true // override version artifacts already published
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}